dependencies {
    testCompile 'junit:junit:4.13'
    compile 'org.yaml:snakeyaml:1.26'
    compile 'com.google.code.gson:gson:2.8.6'
    compileOnly 'org.jetbrains:annotations:20.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from("src/main/resources") {
        include "plugin.properties"
        expand(
                version: "${project.parent.version}",
                name: project.parent.name,
        )
    }
}

//noinspection GroovyAssignabilityCheck
jar.archiveFileName = "plotsquared-api-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/plotsquared/plotsquared-api/" + project.parent.version)
task createPom {
    pom {
        project {
            groupId = rootProject.group
            artifactId = "plotsquared-api"
            version = rootProject.version
        }
    }.writeTo("../mvn/com/plotsquared/plotsquared-api/${project.parent.version}/plotsquared-api-${project.parent.version}.pom")
    pom {
        project {
            groupId = rootProject.group
            artifactId = "plotsquared-api"
            version = "latest"
        }
    }.writeTo("../mvn/com/plotsquared/plotsquared-api/latest/plotsquared-api-latest.pom")
     .writeTo("pom.xml")
}

task copyFiles {
    doLast {
        copy {
            from "../mvn/com/plotsquared/plotsquared-api/${project.parent.version}/"
            into '../mvn/com/plotsquared/plotsquared-api/latest/'
            include('*.jar')
            rename ("plotsquared-api-${project.parent.version}.jar", 'plotsquared-api-latest.jar')
        }
    }
}

build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)