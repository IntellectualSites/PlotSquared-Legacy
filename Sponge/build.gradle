buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven { url = "http://repo.minecrell.net/releases" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:4.0.15'
    }
}

plugins {
    id "java"
    id "org.spongepowered.plugin" version "0.9.0"
    id "net.minecrell.vanillagradle.server" version "2.2-6"
}

dependencies {
    compile(project(":Core"))
    compile("org.spongepowered:spongeapi:7.2.0")

    implementation(project(":Core"))
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        name = 'minecrell'
        url = 'http://repo.minecrell.net/releases'
    }
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

minecraft {
    version = "1.12.2"
    mappings = "stable_39"
    runDir = 'run'
}

project.archivesBaseName = "${project.archivesBaseName}-mc${minecraft.version}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version,
                'mcVersion': project.minecraft.version
    }
}

jar.archiveFileName = "plotsquared-spongeapi-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/plotsquared/plotsquared-spongeapi/" + project.parent.version)
task createPom {
    doLast {
        pom {
            project {
                groupId = rootProject.group
                artifactId = "plotsquared-spongeapi"
                version = rootProject.version
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-spongeapi/${project.parent.version}/plotsquared-spongeapi-${project.parent.version}.pom")
        pom {
            project {
                groupId = rootProject.group
                artifactId = "plotsquared-spongeapi"
                version = "latest"
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-spongeapi/latest/plotsquared-spongeapi-latest.pom")
         .writeTo("pom.xml")
    }
}

task copyFiles {
    doLast {
        copy {
            from("../mvn/com/plotsquared/plotsquared-spongeapi/${project.parent.version}/")
            into("../mvn/com/plotsquared/plotsquared-spongeapi/latest/")
            include("plotsquared-spongeapi*.jar")
            rename("plotsquared-spongeapi-${project.parent.version}.jar", "plotsquared-spongeapi-latest.jar")
        }
    }
}

shadowJar {
    dependencies {
        include(dependency(':Core'))
    }
    archiveFileName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDirectory = file "../target"
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn(shadowJar)
build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
