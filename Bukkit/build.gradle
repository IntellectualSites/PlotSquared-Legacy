repositories {
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    mavenLocal()
}

dependencies {
    implementation(project(":Core"))
    compile(project(":Core"))
    implementation("org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT")
    implementation("net.milkbowl.vault:VaultAPI:1.7") {
        exclude module: 'bukkit'
    }
    implementation("me.clip:placeholderapi:2.10.4")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from("src/main/resources") {
        include "plugin.yml"
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

//noinspection GroovyAssignabilityCheck
jar.archiveFileName = "plotsquared-bukkitapi-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/plotsquared/plotsquared-bukkitapi/" + project.parent.version)
task createPom {
    doLast {
        pom {
            project {
                groupId = rootProject.group
                artifactId = "plotsquared-bukkitapi"
                version = rootProject.version
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-bukkitapi/${project.parent.version}/plotsquared-bukkitapi-${project.parent.version}.pom")
        pom {
            project {
                groupId = "com.plotsquared"
                artifactId = "plotsquared-bukkitapi"
                version = "latest"
            }
        }.writeTo("../mvn/com/plotsquared/plotsquared-bukkitapi/latest/plotsquared-bukkitapi-latest.pom")
         .writeTo("pom.xml")
    }
}

task copyFiles {
    doLast {
        copy {
            from("../mvn/com/plotsquared/plotsquared-bukkitapi/${project.parent.version}/")
            into("../mvn/com/plotsquared/plotsquared-bukkitapi/latest/")
            include("plotsquared-bukkitapi*.jar")
            rename("plotsquared-bukkitapi-${project.parent.version}.jar", "plotsquared-bukkitapi-latest.jar")
        }
    }
}

shadowJar {
    dependencies {
        include(dependency(":Core"))
    }
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file "../target"
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar)
build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
